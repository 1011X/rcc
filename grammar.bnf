<id> ::= /[A-Za-z_][A-Za-z0-9_]*/

<num> ::=
	/[-+]?[1-9][0-9_]*/     ; base   10
	| /0x[A-Fa-f0-9_]+/     ; base 0x10
	| /0b[01_]+/            ; base 0b10

<type> ::= <id> ['<' <type> {',' <type>} '>']
	| 'bool'
	| 'u16' | 'i16' | 'usize' | 'isize'
	| '[' <type> ';' <num> ']'
	| '^' <type>
	| 'proc' '(' [<type> {',' <type>}] ')'

<param> ::= ['var'] <id> ':' <type>

<bin-op> ::= '<' | '>' | '<=' | '>=' | '=' | '!=' | '|' | '&' | '^'
<bin-expr> ::= <factor> <bin-op> <factor>

<lval> ::= <id> {'*' | '[' <factor> ']'}

<lit> ::= <num>
<factor> ::= <lit> | <lval>

<stmt> ::=
	'let' <id> [':' <type>] '=' <expr>
	| 'var' <id> [':' <type>] '=' <expr>
	| 'drop' <id> '=' <expr>
	
	| '!' <lval>
	| '-' <lval>
	
	| <lval> '<<=' <factor>
	| <lval> '>>=' <factor>
	
	| <lval> '^=' <factor> '&' <factor>
	| <lval> '^=' <factor> {'^' <factor>}
	
	| <lval> '+=' <factor> {'+' <factor> | '-' <factor>}
	| <lval> '-=' <factor> {'+' <factor> | '-' <factor>}
	
	| <lval> '<>' <lval>
	| <factor> '?' <lval> '<>' <lval>
	
	| 'if' <bin-expr> <block>
	  ['else' <block>]
	  'fi' <bin-expr>
	
	| 'from'  <bin-expr> [<block>]
	  'until' <bin-expr> [<block>]
	
	| 'call' <lval> '(' [<factor> {',' <factor>}] ')'
	| 'uncall' <lval> '(' [<factor> {',' <factor>}] ')'

<block> ::= '{' { <stmt> ';' } '}'

<proc> ::=
	'proc' <id> '(' [<param> {',' <param>}] ')' <block>

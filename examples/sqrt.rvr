~[
 * Calculating the square root of an integer.
 * Adapted version of example from "Janus: a time-reversible language" by
 * C. Lutz and H. Derby.
 ]~

~ Calculates floor(sqrt(num))
proc root(var num: int, var root: int)
    let bit := 1
    
    from bit = 1
    until bit ^ 2 > num   ~ find exponential ball park
        do doublebit: bit
    loop

    from bit ^ 2 > num
        undo doublebit: bit
        if (root + bit) ^ 2 <= num
            root += bit
        fi root / bit mod 2 != 0
    until bit = 1
    loop
    
    drop bit
    num -= root ^ 2
end

proc doublebit(var bit: int)
    let z := bit
    bit += z
    drop z := bit / 2
end

proc main
    let num := 66
    let root := 0
    do root: num, root
    drop root := 8
    drop num := 2
    ~[
    root += 25
    undo root: num, root
    ]~
end

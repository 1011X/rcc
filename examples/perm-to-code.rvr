~ Based on David Gries: The science of programming, 1987. pp.270

~ Permutation to code
~ Given an array X that contains a permutation of the integers 0, 1, ...,
~ size(X)-1, it computes the array X' where X'[i] is the number of X[j < i] that
~ are smaller than X[i].

proc perm_to_code(var x: int[])
    from var k := size(x)
    until drop k := 0
        k -= 1
        from var j := 0
        until drop j := k
            if x[j] > x[k]
                x[j] -= 1
            fi x[j] >= x[k]
            j += 1
        loop
    loop
end

proc main
    var x := [2, 0, 3, 1, 5, 4]
    do show: x
    do perm_to_code: x
end
